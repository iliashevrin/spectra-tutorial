import "DwyerPatterns.spectra"
 
spec Elevator
 
type Floors = Int(0..10);
env Floors elevatorLocation;
env Floors request;
sys {UP, DOWN} command;
 
define
  requestedUp := request > elevatorLocation;
  requestedDown := request < elevatorLocation;
  openRequest := request != elevatorLocation;
 
// maintain requests while not answered
asm alw elevatorLocation != request -> next(request) = request;
 
// instantiate response pattern for eventually reaching the 
// requested floor
asm S_responds_to_P_globally(elevatorLocation = request,
							requestedUp & command = UP);
asm S_responds_to_P_globally(elevatorLocation = request, 
							requestedDown & command = DOWN);
 
asm moveRightDirection:
  alw (command=UP -> next(elevatorLocation) >= elevatorLocation) &
	(command=DOWN -> next(elevatorLocation) <= elevatorLocation);
    	
gar eventuallyHandleOpenRequests:
  S_responds_to_P_globally(request=elevatorLocation, openRequest);
